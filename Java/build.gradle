buildscript {
        repositories {
                mavenCentral()
                maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        }

        dependencies {
                classpath "net.saliman:gradle-cobertura-plugin:2.5.1"
        }
}

plugins {
        id 'net.saliman.cobertura' version '2.5.1'
		id "de.undercouch.download" version "3.4.3"
}

apply plugin: 'net.saliman.cobertura'

subprojects {
	apply plugin: 'java'

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

	apply plugin: 'eclipse'

	repositories {
	    mavenCentral()
	}
}

//=========================================================================================
//
// RELEASE VERSION, RELEASE DATE AND BINARY PACK INFORMATION ARE 
// CONTROLLED BY THE SECTION BELOW
//
//=========================================================================================

ext.YEAR = '2019'

ext.BuildDate = 'Fri Nov 15 12:25:01 CDT ' + "$YEAR"
ext.SpecificationVersion = '3.4.0.1'
ext.SpecificationVersionShort = '3.4.0'

// Comment out line below to publish offical release
// ext.SpecificationVersion = "$SpecificationVersion" + "-SNAPSHOT"

// Update below to L1.all.rrg, G1.all.gload, or E1.all.eload when needed
ext.etaImplementationVersion = 'etaj' + "$SpecificationVersionShort" + '.G1.all.rrg'
ext.emaImplementationVersion = 'emaj' + "$SpecificationVersionShort" + '.G1.all.rrg'

// NOTE! update with new asset version
ext.BINARY_PACK_VERSION_TO_DOWNLOAD = '1.4.0.L1'

//=========================================================================================
//
// END OF SECTION CONTROLLING THE RELEASE INFORMATION
//
//=========================================================================================


ext.vendor = 'Thomson Reuters'
ext.javadoc_footer = '<a href="https://developers.thomsonreuters.com/" target=_top>Thomson Reuters</a>'
ext.javadoc_header = '<a href="https://developers.thomsonreuters.com/" target=_top>Thomson Reuters</a>'
ext.javadoc_bottom = '<i>Copyright @ ' + "$YEAR" + ' Thomson Reuters. All Rights Reserved.</i>'

task downloadBinaryPack(type: Download) {
    description 'This task downloads the Elektron-SDK-BinaryPack file from GitHub.'

	def releaseToDownload = "Elektron-SDK-BinaryPack-" + BINARY_PACK_VERSION_TO_DOWNLOAD
	def zipFileToDownload = releaseToDownload + ".zip"
	src 'https://github.com/Refinitiv/Elektron-SDK/releases/download/Elektron-SDK-' + BINARY_PACK_VERSION_TO_DOWNLOAD + '/' + zipFileToDownload
	dest new File('../.', 'Elektron-SDK-BinaryPack.zip')
}

task unzipBinaryPack(dependsOn: downloadBinaryPack, type: Copy) {
	ext.temp = new File('../temp')
	from zipTree(downloadBinaryPack.dest)
	into ext.temp
}

task getBinaryPack () {

    // check if we are in GSG package
    File gsgDir = file('../Elektron-SDK-BinaryPack')
    if (!gsgDir.exists()) {
		dependsOn unzipBinaryPack
    }	
    else
    {
        println "Elektron-SDK-BinaryPack already exists, skip downloading."
    }	
		
	doLast {
		// check if Binary Pack exists
		File gsgDirNew = file('../Elektron-SDK-BinaryPack')
		if (!gsgDirNew.exists()) {
		
			// move the file
			file('../temp/Elektron-SDK-BinaryPack').renameTo(file('../Elektron-SDK-BinaryPack'))
			// delete temp directory and downloaded file
			unzipBinaryPack.ext.temp.deleteDir()
			downloadBinaryPack.dest.delete()
		}		
	}
}

allprojects {
	dependencies {

		compile group: 'commons-codec', name: 'commons-codec', version: '1.4'
		compile group: 'commons-logging', name: 'commons-logging', version: '1.1.1'

		compile group: 'junit', name: 'junit', version: '4.12'

		testCompile group: 'junit', name: 'junit', version: '4.12'
	}

	compileJava {
	        options.compilerArgs += ["-Xlint:cast"]
	        options.compilerArgs += ["-Xlint:deprecation"]
	        options.compilerArgs += ["-Xlint:divzero"]
	        options.compilerArgs += ["-Xlint:empty"]
	        options.compilerArgs += ["-Xlint:fallthrough"]
	        options.compilerArgs += ["-Xlint:finally"]
	        options.compilerArgs += ["-Xlint:overrides"]
	        options.compilerArgs += ["-Xlint:path"]
	        options.compilerArgs += ["-Xlint:serial"]
	        options.compilerArgs += ["-Xlint:unchecked"]
	}

        tasks.withType( JavaCompile ) {
				dependsOn getBinaryPack
                options.fork = true
                options.incremental = true
        }

	// set the jvmArgs and commandLineArgs for all applications
	tasks.withType ( JavaExec ) {
	        if ( project.hasProperty("vmArgs") ) {
        	        jvmArgs Eval.me( buildArgsList( vmArgs ) )
        	}

	        if ( project.hasProperty("commandLineArgs") ) {
        	        args Eval.me( buildArgsList( commandLineArgs ) )
       		}
	}

	// set the jvmArgs for junit
	tasks.withType ( Test ) {
	        if ( project.hasProperty("vmArgs") ) {
        	        jvmArgs Eval.me( buildArgsList( vmArgs ) )
        	}

		minHeapSize = "1024m"
		maxHeapSize = "2048m"
	}
}

wrapper {
	gradleVersion = '5.4.1'
}

task uploadAll ( ) {
	group 'Upload'
	description 'Uploads All artifacts to maven central, run with -Pmavencentral'
	if (project.hasProperty("mavencentral"))
	{
		dependsOn ':Eta:AnsiPage:uploadArchives'
		dependsOn ':Eta:Core:uploadArchives'
		dependsOn ':Eta:ValueAdd:uploadArchives'
		dependsOn ':Eta:ValueAddCache:uploadArchives'
		dependsOn ':Ema:Core:uploadArchives'
	}
	else
	{
		doLast {
		println "" 
		println "/////////////////////////////////////////////////////////"
		println "" 
		println "This task needs to be run with -Pmavencentral option"
		println "Exiting without publishing to Maven Central"
		println "" 
		println "/////////////////////////////////////////////////////////"
		println "" 
		}
	}
}

// this method creates a list of arguments that are used in setting the jvmArgs and commandLineArgs
def buildArgsList ( options ) {
	// remove spaces 
	def arguments = options.tokenize()

		// create a string that can be used by Eval 
		def cla = "["
		// go through the list to get each argument
		arguments.each {
			cla += "'" + "${it}" + "',"
		}

	// remove last "," add "]" and set the args 
	return cla.substring( 0, cla.lastIndexOf(',') ) + "]"
}

// can't use a dynamic date for manifest since it triggers a build each time even when nothing changed
def getDate() {
	def date = new Date()
	def formattedDate = date.format('E MMM dd HH:mm:ss z yyyy ')
	return formattedDate
}
// disable creating empty Java.jar file 
jar.enabled = false
