apply plugin: 'net.saliman.cobertura'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java-library'

def jenkinsBuild = System.getenv("BUILD_NUMBER") ?: "0"

group = "com.refinitiv.ema"
archivesBaseName = "ema"
version "$SpecificationVersion"

java {
    withSourcesJar()
}

ext.sharedManifest = manifest {
    attributes( 'Sealed': 'true',
            'Build-Date': BuildDate,
            'Specification-Version': SpecificationVersion,
            'Specification-Vendor': vendor,
            'Implementation-Title': group,
            'Implementation-Version': emaImplementationVersion,
            'Implementation-Vendor': vendor )
}

task packageTests(type: Jar) {
	group = 'Build'
	description 'Assembles a jar archive containing the unit test classes and test utilities.'
    archiveVersion.convention("$SpecificationVersion");
    archiveVersion.set("$SpecificationVersion");
    destinationDirectory.set(file("../Libs/"));
    archiveClassifier.convention('tests');
    archiveClassifier.set('tests');
    manifest {
        attributes 'Specification-Title': 'Enterprise Message API (EMA) Tests Java Edition'
        from sharedManifest
    }
    from sourceSets.test.output
}
packageTests.dependsOn compileTestJava
tasks.build.dependsOn tasks.packageTests

compileTestJava {
    options.compilerArgs += ["-Xlint:cast"]
    options.compilerArgs += ["-Xlint:deprecation"]
    options.compilerArgs += ["-Xlint:divzero"]
    options.compilerArgs += ["-Xlint:empty"]
    options.compilerArgs += ["-Xlint:fallthrough"]
    options.compilerArgs += ["-Xlint:finally"]
    options.compilerArgs += ["-Xlint:overrides"]
    options.compilerArgs += ["-Xlint:path"]
    options.compilerArgs += ["-Xlint:serial"]
    options.compilerArgs += ["-Xlint:unchecked"]
}

javadoc {
    doFirst {
        println "Generating javadoc for EMA"
    }

    exclude '**/*Impl.java'
    exclude '**/JUnit*.java'

    destinationDir = file('../Docs/refman/emajava/')
    title = "Enterprise Message API (EMA) " +  "${SpecificationVersion}" + " - Java Edition"

    options.footer ("${javadoc_footer}")
    options.header ("${javadoc_header}")
    options.bottom ("${javadoc_bottom}")

    options.group ('Access', 'com.refinitiv.ema.access')
    options.group ('RDM', 'com.refinitiv.ema.rdm')
    options.group ('Login', 'com.refinitiv.ema.domain.login')

    options.setWindowTitle ('EMA')
}

task javadocJar (type: Jar, dependsOn: this.javadoc) {
    archiveClassifier.convention('javadoc');
    archiveClassifier.set('javadoc');
    from javadoc.destinationDir
    destinationDirectory.set(jar.destinationDirectory)

    manifest {
        attributes 'Specification-Title': 'Enterprise Message API (EMA) Java Edition'
        from sharedManifest
    }
}

publishing {
    publications{
        mavenJava(MavenPublication){

            groupId = group
            artifactId = archivesBaseName
            version = "$SpecificationVersion"
            from components.java
            artifact javadocJar
            artifact packageTests

            pom {
                name = "$archivesBaseName"
                packaging = 'jar'
                url = 'https://github.com/Refinitiv/Real-Time-SDK'
                description = 'Enterprise Message API (EMA) Java Edition'

                scm {
                    connection = 'scm:git:git://github.com/Refinitiv/Real-Time-SDK.git'
                    developerConnection = 'scm:git:ssh://github.com:Refinitiv/Real-Time-SDK.git'
                    url = 'http://github.com/Refinitiv/Real-Time-SDK/tree/master'
                }

                developers {
                    developer {
                        organization = 'Refinitiv'
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
            name = "repository"
            def releaseRepository = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepository = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith('SNAPSHOT') ? snapshotRepository : releaseRepository
        }
    }
}

signing {
    setRequired {
        // signing is only required if the artifacts are to be published
        gradle.taskGraph.allTasks.any { it.equals( PublishToMavenRepository) }
    }
    sign configurations.archives
    sign publishing.publications.mavenJava
}

cobertura {
     coverageFormats = ['html', 'xml']
}

eclipse {
    project {
        name = 'RTSDK-Ema-Src'
    }
}

dependencies {
    api 'commons-logging:commons-logging:1.2'
    api group: 'org.apache.commons', name: 'commons-configuration2', version: '2.9.0'
    api group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    api group: 'org.slf4j', name: 'slf4j-api', version: '2.0.7'

    api project(':Eta:ValueAdd')
    api project(':Eta:Core')

    testRuntimeOnly 'org.hamcrest:hamcrest-core:1.3'
}

jar {
	doFirst { println 'Creating EMA jar file' }

	manifest { 
		attributes 'Specification-Title': 'Enterprise Message API (EMA) Java Edition'
				from sharedManifest 
	}

    destinationDirectory.set(file("../Libs/"))
    archiveBaseName.convention('ema');
    archiveBaseName.set('ema');
}

task cleanJar (type: Delete) {
        delete "${jar.archiveFile}"
}

task cleanJavadoc (type: Delete) {
	delete ('../Docs/refman')
}

task cleanJavadocJar (type: Delete) {
	delete "${jar.destinationDirectory}/ema-javadoc.jar"
}

clean.dependsOn cleanJar, cleanJavadoc, cleanJavadocJar

test {
	// exclude the utility class and the suite class
	exclude '**/TestUtilities.class'
	exclude '**/RunAllTests.class'
	exclude '**/JUnitTestConnect.class'
    forkEvery = 1
    // set to true to see the junit stdoutput
    testLogging.showStandardStreams = false
    maxParallelForks = 1
}

repositories {
    mavenCentral()
}