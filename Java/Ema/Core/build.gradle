plugins {
    id 'maven-publish'
    id 'java-library'
    id 'org.jreleaser' version '1.18.0'
}

def jenkinsBuild = System.getenv("BUILD_NUMBER") ?: "0"

group = "com.refinitiv.ema"
archivesBaseName = "ema"
version "$SpecificationVersion"

java {
    withSourcesJar()
}

ext.sharedManifest = manifest {
    attributes( 'Sealed': 'true',
            'Build-Date': BuildDate,
            'Specification-Version': SpecificationVersion,
            'Specification-Vendor': vendor,
            'Implementation-Title': group,
            'Implementation-Version': emaImplementationVersion,
            'Implementation-Vendor': vendor )
}

task packageTests(type: Jar) {
	group = 'Build'
	description 'Assembles a jar archive containing the unit test classes and test utilities.'
    archiveVersion.convention("$SpecificationVersion");
    archiveVersion.set("$SpecificationVersion");
    destinationDirectory.set(file("../Libs/"));
    archiveClassifier.convention('tests');
    archiveClassifier.set('tests');
    manifest {
        attributes 'Specification-Title': 'Enterprise Message API (EMA) Tests Java Edition'
        from sharedManifest
    }
    from sourceSets.test.output
}
packageTests.dependsOn compileTestJava
tasks.build.dependsOn tasks.packageTests

compileTestJava {
    options.compilerArgs += ["-Xlint:cast"]
    options.compilerArgs += ["-Xlint:deprecation"]
    options.compilerArgs += ["-Xlint:divzero"]
    options.compilerArgs += ["-Xlint:empty"]
    options.compilerArgs += ["-Xlint:fallthrough"]
    options.compilerArgs += ["-Xlint:finally"]
    options.compilerArgs += ["-Xlint:overrides"]
    options.compilerArgs += ["-Xlint:path"]
    options.compilerArgs += ["-Xlint:serial"]
    options.compilerArgs += ["-Xlint:unchecked"]
    options.compilerArgs += ["-Xlint:-options"] //suppresses warning "warning: [options] source/target value 8 is obsolete and will be removed in a future release"
}

javadoc {
    doFirst {
        println "Generating javadoc for EMA"
    }

    exclude '**/*Impl.java'
    exclude '**/JUnit*.java'

    destinationDir = file('../Docs/refman/emajava/')
    title = "Enterprise Message API (EMA) " +  "${SpecificationVersion}" + " - Java Edition"

    options.footer ("${javadoc_footer}")
    options.header ("${javadoc_header}")
    options.bottom ("${javadoc_bottom}")

    options.group ('Access', 'com.refinitiv.ema.access')
    options.group ('RDM', 'com.refinitiv.ema.rdm')
    options.group ('Login', 'com.refinitiv.ema.domain.login')

    options.setWindowTitle ('EMA')
}

task javadocJar (type: Jar, dependsOn: this.javadoc) {
    archiveClassifier.convention('javadoc');
    archiveClassifier.set('javadoc');
    from javadoc.destinationDir
    destinationDirectory.set(jar.destinationDirectory)

    manifest {
        attributes 'Specification-Title': 'Enterprise Message API (EMA) Java Edition'
        from sharedManifest
    }
}

publishing {
    publications{
        mavenJava(MavenPublication){

            groupId = group
            artifactId = archivesBaseName
            version = "$SpecificationVersion"
            from components.java
            artifact javadocJar
            artifact packageTests

            pom {
                name = "$archivesBaseName"
                packaging = 'jar'
                url = 'https://github.com/Refinitiv/Real-Time-SDK'
                description = 'Enterprise Message API (EMA) Java Edition'

                scm {
                    connection = 'scm:git:git://github.com/Refinitiv/Real-Time-SDK.git'
                    developerConnection = 'scm:git:ssh://github.com:Refinitiv/Real-Time-SDK.git'
                    url = 'http://github.com/Refinitiv/Real-Time-SDK/tree/master'
                }

                developers {
                    developer {
                        organization = 'LSEG'
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "PreDeploy"
            url = uri(layout.buildDirectory.dir("pre-deploy"))
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                    stagingRepository('build/pre-deploy')
                }
            }
        }
    }

    release {
        github {
            enabled = false
        }
    }
}

eclipse {
    project {
        name = 'RTSDK-Ema-Src'
    }
}

dependencies {
    api 'commons-logging:commons-logging:1.3.5'
    api group: 'org.apache.commons', name: 'commons-configuration2', version: '2.12.0'
    api group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
    api group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'

    api project(':Eta:ValueAdd')
    api project(':Eta:Core')

    testRuntimeOnly 'org.hamcrest:hamcrest-core:3.0'
}

jar {
	doFirst { println 'Creating EMA jar file' }

	manifest { 
		attributes 'Specification-Title': 'Enterprise Message API (EMA) Java Edition'
				from sharedManifest 
	}

    destinationDirectory.set(file("../Libs/"))
    archiveBaseName.convention('ema');
    archiveBaseName.set('ema');
}

task cleanJar (type: Delete) {
        delete "${jar.archiveFile}"
}

task cleanJavadoc (type: Delete) {
	delete ('../Docs/refman')
}

task cleanJavadocJar (type: Delete) {
	delete "${jar.destinationDirectory}/ema-javadoc.jar"
}

clean.dependsOn cleanJar, cleanJavadoc, cleanJavadocJar

test {
	// exclude the utility class and the suite class
	exclude '**/TestUtilities.class'
	exclude '**/RunAllTests.class'
	exclude '**/JUnitTestConnect.class'
    forkEvery = 1
    // set to true to see the junit stdoutput
    testLogging.showStandardStreams = false
    maxParallelForks = 1
}

repositories {
    mavenCentral()
}