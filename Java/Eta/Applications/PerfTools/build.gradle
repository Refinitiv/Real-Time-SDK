apply plugin: 'application'

eclipse {
        project {
                name = 'RTSDK-Eta-PerfTools'
        }
}

dependencies {
	// version 1.1.3_8 not available on mavencentral
    implementation group: 'xpp3', name: 'xpp3', version: '1.1.4c'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.19.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.19.1'

    implementation project(':Eta:Applications:Shared')
    implementation project(':Eta:Core')
    implementation project(':Eta:ValueAdd')
    implementation project(':Eta:Converter')
}

mainClassName = 'com.refinitiv.eta.perftools.provperf.ProvPerf'

task copyDictionary{
    outputs.files files('../../../etc')
            .asFileTree
            .files
            .collect{'./${it.name}'}

    doLast{
        copy{
            from '../../../etc'
            into './'
        }
    }
}

task deleteDictionary( type: Delete ) {
	delete "$projectDir/RDMFieldDictionary"
	delete "$projectDir/RDMFieldDictionary_large"
	delete "$projectDir/enumtype.def"
}

def JVM_OPTIONS = "-server -XX:+ForceTimeHighResolution -Xms2048m -Xmx2048m"
def APP_ARGS
def exportedProjects= [":Eta:Applications:Shared" , ":Eta:Applications:PerfTools" ]

javadoc {
    doFirst {
        println "Generating javadoc for ETA PerfTools"
    }

    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files( exportedProjects.collect { project(it).sourceSets.main.compileClasspath } )

    destinationDir = file('../../Docs/refman/etajperftools')
    title = "Enterprise Transport API (ETA) " +  "${SpecificationVersion}" + " - Java Edition Performance Tools Examples"

    options.footer ("${javadoc_footer}")
    options.header ("${javadoc_header}")
    options.bottom ("${javadoc_bottom}")

    options.setWindowTitle ('ETA Performance Tools')
}

run.dependsOn copyDictionary
clean.dependsOn deleteDictionary

task runETAPerfProvider(type: JavaExec) {
    dependsOn 'copyDictionary'
    group 'Run ETA Performance Tools Examples'
    description "Run ETAJ OMM Provider performance application"

    if (JVM_OPTIONS)
        jvmArgs Eval.me ( buildArgsList ( JVM_OPTIONS ) )

    // command line options
    //.............................
    // - uncomment and add to APP_ARGS to specify the command line options
    // APP_ARGS="-runTime 60 -latencyUpdateRate 1000 -tickRate 1000 -updateRate 100000"

    if (APP_ARGS)
        args Eval.me ( buildArgsList ( APP_ARGS ) ) 

    mainClass.set('com.refinitiv.eta.perftools.provperf.ProvPerf')
    classpath = sourceSets.main.runtimeClasspath
}

task runETAPerfConsumer(type: JavaExec) {
    dependsOn 'copyDictionary'
    group 'Run ETA Performance Tools Examples'
    description "Run ETAJ OMM Consumer performance application"

    if (JVM_OPTIONS)
        jvmArgs Eval.me ( buildArgsList ( JVM_OPTIONS ) ) 

    // command line options
    //.............................
    // - uncomment and add to APP_ARGS to specify the command line options
    // APP_ARGS = "-steadyStateTime 90 -tickRate 1000 -itemCount 100000"

    if (APP_ARGS)
        args Eval.me ( buildArgsList ( APP_ARGS ) ) 

    mainClass.set('com.refinitiv.eta.perftools.consperf.ConsPerf')
    classpath = sourceSets.main.runtimeClasspath
}

task runETAPerfNIProvider(type: JavaExec) {
    dependsOn 'copyDictionary'
    group 'Run ETA Performance Tools Examples'
    description "Run ETAJ OMM NIProvider performance application"

    if (JVM_OPTIONS)
        jvmArgs Eval.me ( buildArgsList ( JVM_OPTIONS ) )

    // command line options
    //.............................
    // - uncomment and add to APP_ARGS to specify the command line options
    // APP_ARGS="-runTime 60 -latencyUpdateRate 1000 -tickRate 1000 -updateRate 100000"

    if (APP_ARGS)
        args Eval.me ( buildArgsList ( APP_ARGS ) )

    mainClass.set('com.refinitiv.eta.perftools.niprovperf.NIProvPerf')
	classpath = sourceSets.main.runtimeClasspath
}

task runETAPerfTransport(type: JavaExec) {
    dependsOn 'copyDictionary'
    group 'Run ETA Performance Tools Examples'
    description "Run ETAJ Transport performance application"

    if (JVM_OPTIONS)
        jvmArgs Eval.me ( buildArgsList ( JVM_OPTIONS ) )

    // command line options
    //.............................
    // - uncomment and add to APP_ARGS to specify the command line options
    // APP_ARGS="-msgRate 1000 -runTime 60"

    mainClass.set('com.refinitiv.eta.perftools.transportperf.TransportPerf')
	classpath = sourceSets.main.runtimeClasspath
}

