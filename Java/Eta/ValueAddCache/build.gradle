plugins {
    id 'maven-publish'
    id 'java-library'
    id 'org.jreleaser' version '1.18.0'
}

def jenkinsBuild = System.getenv("BUILD_NUMBER") ?: "0"

group = "com.refinitiv.eta.valueadd.cache"
archivesBaseName = "etaValueAddCache"
version "$SpecificationVersion"

java {
    withSourcesJar()
}

ext.sharedManifest = manifest {
    attributes( 'Sealed': 'true',
            'Build-Date': BuildDate,
            'Specification-Version': SpecificationVersion,
            'Specification-Vendor': vendor,
            'Implementation-Title': group,
            'Implementation-Version': etaImplementationVersion,
            'Implementation-Vendor': vendor )
}

javadoc {
    doFirst {
        println "Generating javadoc for ValueAddCache"
    }

    destinationDir = file('../Docs/refman/etajvalueaddcache/')
    title = "ETA ValueAddCache JAVA " +  "${SpecificationVersion}"

    options.footer ("${javadoc_footer}")
    options.header ("${javadoc_header}")
    options.bottom ("${javadoc_bottom}")

    options.group ('Cache', 'com.refinitiv.eta.valueadd.cache')

    options.setWindowTitle ('ETA ValueAddCache')
}

task javadocJar (type: Jar, dependsOn: this.javadoc) {
    archiveClassifier.convention('javadoc');
    archiveClassifier.set('javadoc');
    from javadoc.destinationDir
    destinationDirectory.set(jar.destinationDirectory)

    manifest {
        attributes 'Specification-Title': 'Enterprise Transport API (ETA) Java Edition ValueAddCache Classes'
        from sharedManifest
    }
}

publishing {
    publications{
        mavenJava(MavenPublication){

            groupId = group
            artifactId = archivesBaseName
            version = "$SpecificationVersion"
            from components.java
            artifact javadocJar

            pom {
                name = "$archivesBaseName"
                packaging = 'jar'
                url = 'https://github.com/Refinitiv/Real-Time-SDK'
                description = 'Enterprise Transport API (ETA) Java Edition ValueAddCache Classes'

                scm {
                    connection = 'scm:git:git://github.com/Refinitiv/Real-Time-SDK.git'
                    developerConnection = 'scm:git:ssh://github.com:Refinitiv/Real-Time-SDK.git'
                    url = 'http://github.com/Refinitiv/Real-Time-SDK/tree/master'
                }

                developers {
                    developer {
                        organization = 'LSEG'
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "PreDeploy"
            url = uri(layout.buildDirectory.dir("pre-deploy"))
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                    stagingRepository('build/pre-deploy')
                }
            }
        }
    }

    release {
        github {
            enabled = false
        }
    }
}

eclipse {
    project {
        name = 'RTSDK-Eta-ValueAddCache'
    }
}

dependencies {
    testImplementation('org.mockito:mockito-core:5.18.0'){
        exclude group: 'org.hamcrest'
    }

    api project(':Eta:Core')
    api project(':Eta:ValueAdd')
}

jar {
	doFirst { println 'Creating ValueAddCache jar file' }

	manifest { 
		attributes 'Specification-Title': 'Enterprise Transport API (ETA) Java Edition ValueAddCache Classes'
		from sharedManifest 
	}
    destinationDirectory.set(file("../Libs/"))
    archiveBaseName.convention('etaValueAddCache');
    archiveBaseName.set('etaValueAddCache');
}

task cleanJar (type: Delete) {
        delete "${jar.archivePath}"
}
clean.dependsOn cleanJar

